plugins {
    id 'application'
    id 'org.beryx.jlink' version '2.24.1'
    id 'info.solidsoft.pitest' version '1.7.0'
    id 'org.openjfx.javafxplugin' version '0.0.11'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group 'jasmine.jragon'
    version '0.23.3'

    sourceCompatibility = '16'
    targetCompatibility = '16'

    repositories {
        mavenCentral()
    }

    compileJava {
        options.encoding("UTF-8")
    }

    compileTestJava {
        options.encoding("UTF-8")
    }

    dependencies {
        implementation 'org.javatuples:javatuples:1.2'
        implementation 'org.jetbrains:annotations:22.0.0'

        testImplementation 'org.testng:testng:7.5'
        testImplementation 'org.slf4j:slf4j-api:1.7.32'
        testImplementation 'org.slf4j:slf4j-simple:1.7.32'
        testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.8.2'
    }
}

subprojects {
    dependencies {
        implementation 'com.opencsv:opencsv:5.5.2'
        implementation 'org.jgrapht:jgrapht-ext:1.5.1'
    }
}

application {
    mainModule = 'bomb'
    mainClass = 'bomb.HelloApplication'
//    mainClass = 'bomb.Main'
}

javafx {
    version = '16'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation project(':core')
    implementation project(':utility')

    implementation('com.jfoenix:jfoenix:9.0.4') {
        exclude group: "org.javafx"
    }
    implementation('io.github.palexdev:materialfx:11.12.0') {
        exclude group: "org.javafx"
    }
}

test {
    useTestNG()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}